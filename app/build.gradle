apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.kiegame.mobile"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        flavorDimensions "versionCode"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-debug"
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    dexOptions {
        preDexLibraries = false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
        dev {
            resValue('string', 'app_name', '游易电竞-开发版')
            buildConfigField "String", "SERVER_BASE", '"http://10.168.1.200:9000/"'
        }
        beta {
            resValue('string', 'app_name', '游易电竞-测试版')
            buildConfigField "String", "SERVER_BASE", '"http://www.kiegame.com:9000/"'
        }
        pre {
            resValue('string', 'app_name', '游易电竞')
            buildConfigField "String", "SERVER_BASE", '"http://www.lingin.net/"'
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.productFlavors[0].name.endsWith("dev")) {
                outputFileName = "app_dev_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${buildTime()}.apk"
            } else if (variant.productFlavors[0].name.endsWith("beta")) {
                outputFileName = "app_beta_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${buildTime()}.apk"
            } else if (variant.productFlavors[0].name.endsWith("pre")) {
                outputFileName = "app_pre_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${buildTime()}.apk"
            }
        }
    }
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd-HH-mm-ss", TimeZone.getTimeZone("GMT+08:00"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // 日志打印
    implementation 'com.orhanobut:logger:2.2.0'
    // JSON解析
    implementation 'com.google.code.gson:gson:2.8.5'
    // 网络访问
    implementation 'com.squareup.okhttp3:okhttp:4.1.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'
    // 异步任务
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    // banner
    implementation 'com.youth.banner:banner:1.4.10'
    // RecyclerViewAdapter
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.50'
    // 小红点
    implementation 'q.rorbin:badgeview:1.1.3'
    // 图片加载
    implementation 'com.github.bumptech.glide:glide:4.9.0'
}
